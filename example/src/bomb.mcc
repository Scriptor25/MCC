include "std.mcc"

namespace example

define pos(target: string): position {
    return ref<position>(entity, target, "Pos")
}

define on_ground(target: string): number {
    return ref<number>(entity, target, "OnGround")
}

define summon_tnt(target: string) {
    std:summon(pos(target), "minecraft:tnt")
}

global bomb_info => {
    enable: number,
    counter: number,
    target: string,
}

/*
 * drop a live tnt every second if enabled
 */
define bomb_tick() #minecraft:tick {
    if (bomb_info.enable)
        unless (++bomb_info.counter % 20)
            unless (on_ground(bomb_info.target))
                summon_tnt(bomb_info.target)
}

/*
 * enable dropping tnt every second for the specified target
 */
define enable_bomb(target: string) {
    unless (bomb_info)
        bomb_info = {}

    unless (bomb_info.enable) {
        bomb_info.enable = 1
        bomb_info.counter = 0
        bomb_info.target = target
    }
}

/*
 * disable dropping tnt
 */
define disable_bomb() {
    unless (bomb_info)
        return void

    bomb_info.enable = 0
}
